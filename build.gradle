buildscript {
	repositories {
	    maven { url = "https://maven.k-int.com/content/repositories/public" }
		mavenCentral()
			maven { url = "https://plugins.gradle.org/m2/" }
			maven { url = "https://repo.grails.org/grails/core" }
			maven { url = "https://maven.k-int.com/content/repositories/releases" }
	}
	dependencies {
		classpath platform("org.apache.grails:grails-gradle-bom:$grailsVersion")
		classpath "org.apache.grails:grails-gradle-plugins"
		
		classpath 'com.k_int.gradle:k-int-gradle-plugin:3.1.0'
		// classpath "org.grails.plugins:hibernate5:$gormVersion" // Prev 7.3.1
	}
}

plugins {
	id 'antlr'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

repositories {
  maven { url = "https://maven.k-int.com/content/repositories/public" }
  mavenCentral()
  maven { url = "https://repo.grails.org/grails/core/" }
}

//version "0.1"
group = 'com.k_int.grails'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.apache.grails.gradle.grails-plugin'

apply plugin: 'org.apache.grails.gradle.grails-gson'
apply plugin: 'com.k_int.gradle.plugin'
apply plugin: 'com.k_int.gradle.conventional-git'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}

	compileOnly {
		extendsFrom developmentOnly
	}

	testImplementation {
		extendsFrom developmentOnly
		extendsFrom implementation
	}

  groovyCompiler
}

bootRun {
		ignoreExitValue = true
	jvmArgs(
			'-Dspring.output.ansi.enabled=always',
			'-noverify',
			'-XX:TieredStopAtLevel=1',
			'-Xmx1024m')
	sourceResources sourceSets.main
	String springProfilesActive = 'spring.profiles.active'
	systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

sourceSets {
  main {
    resources {
      srcDir 'src/migrations'
    }
  }
}

dependencies {
  implementation platform("org.apache.grails:grails-bom:$grailsVersion")
  compileOnly platform("org.apache.grails:grails-bom:$grailsVersion")
  testImplementation platform("org.apache.grails:grails-bom:$grailsVersion")

  implementation "org.apache.grails:grails-core"
  implementation "org.apache.grails:grails-logging"
  implementation "org.apache.grails:grails-databinding"
  implementation "org.apache.grails:grails-interceptors"
  implementation "org.apache.grails:grails-rest-transforms"
  implementation "org.apache.grails:grails-services"
  implementation "org.apache.grails:grails-url-mappings"
  implementation "org.apache.grails:grails-web-boot"
  implementation "org.apache.grails.async:grails-async-core"
  implementation "org.apache.grails:grails-data-hibernate5-spring-boot"
  implementation "org.apache.grails:grails-events"
  // Do we need this?
  //implementation("org.hibernate:hibernate-core:5.6.15.Final")

  implementation "org.springframework.boot:spring-boot-starter-undertow"

  implementation "org.springframework.boot:spring-boot-autoconfigure"
  implementation "org.springframework.boot:spring-boot-starter-logging"
  implementation "org.springframework.boot:spring-boot-starter-validation"

  implementation "org.apache.grails:grails-micronaut"

  console("org.apache.grails:grails-console")
  runtimeOnly("com.h2database:h2")
  

  // Grails 7 testing support
	testImplementation "org.apache.grails.testing:grails-testing-support-core:$grailsTestingVersion"
	testImplementation "org.apache.grails:grails-testing-support-web:$grailsTestingVersion"
	testImplementation "org.apache.grails:grails-data-hibernate5:$grailsVersion"

	testImplementation("org.apache.grails:grails-testing-support-datamapping")
	testImplementation("org.spockframework:spock-core")
	implementation("org.apache.groovy:groovy-xml:4.0.30")
	testImplementation("org.apache.groovy:groovy-xml")
  integrationTestImplementation("org.apache.groovy:groovy-xml:4.0.30")
  groovyCompiler("org.apache.groovy:groovy-xml:4.0.30")
  compileOnly("junit:junit")


  profile "org.apache.grails.profiles:rest-api-plugin"

  // --------------------------------- Custom Deps Below -----------------------------------//
  implementation "org.apache.grails:grails-views-gson"
	implementation "org.postgresql:postgresql:42.7.4"
	implementation 'org.codehaus.janino:janino:3.1.9' // Needed for conditional logging


	implementation "org.springframework.security:spring-security-core", {
		[
			'spring-aop',
			'spring-beans',
			'spring-context',
			'spring-core',
			'spring-expression'
		].each {
			exclude module: it
		}
	}
	implementation "org.springframework.security:spring-security-web", {
		exclude module: 'spring-web'
	}

	implementation 'com.ibm.icu:icu4j:62.1'

	// Minio for file storage to S3
	implementation "io.minio:minio:8.5.12"
	implementation 'com.squareup.okhttp3:okhttp:4.8.1'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.70'
	implementation 'com.amazonaws:aws-java-sdk-s3:1.12.770'

	// SHould be replaced and rewritten out of this module.
	implementation 'io.github.http-builder-ng:http-builder-ng-core:1.0.4'

	antlr "org.antlr:antlr4:4.7" // use ANTLR version 4

	testImplementation 'com.h2database:h2:2.1.212'
}

/* application {
  mainClass.set("web.toolkit.ce.Application")
} */

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
  withSourcesJar()
  withJavadocJar()
}

tasks.withType(GroovyCompile) {
	configure(groovyOptions) {
		forkOptions.jvmArgs = ['-Xmx1024m']
	}
  groovyClasspath += configurations.groovyCompiler
}

tasks.withType(Test) {
  useJUnitPlatform()
}

tasks.withType(Javadoc) {
	dependsOn tasks.named('compileGsonViews')
	options.addStringOption('Xdoclint:none', '-quiet')
	failOnError = false
	exclude('**/build/**')
}

tasks.named("sourcesJar") {
	dependsOn(tasks.named("generateGrammarSource"))
	dependsOn(tasks.named("generateAstGrammarSource"))
}

// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false


eclipse {
	autoBuildTasks ([
		':processResources',
		':generateGrammarSource'
	])
	synchronizationTasks ([
		':processResources',
		':generateGrammarSource'
	])

	classpath {
		defaultOutputDir = new File(project.buildDir.canonicalPath, 'main')
		file {

			whenMerged { classpath ->

				Set<String> resources = sourceSets.main?.resources?.srcDirs?.collect { it.canonicalPath }

				entries.collect().each {

					if (it.path =='grails-app/conf' && it.output?.toLowerCase().endsWith('test')) {
						classpath.entries.remove( it )
						println "Removed ${it}"
						return
					}

					if (it.hasProperty('output') && it.output.startsWith('bin/')) {
						it.output = 'build/' + it.output.substring(4)
					}

					// Eclude the output of the resources added to the source sets for grails.
					// Instead we add the results of the 'processResources' tasks to the classpath later.
					if (it.hasProperty('dir') && resources.find { res -> it.dir?.canonicalPath?.startsWith(res) } ) {
						it.exported = false
						it.excludes << '**/*'
					}
				}
				def lib = new org.gradle.plugins.ide.eclipse.model.Library(fileReference(file("${tasks.processResources.destinationDir}")))
				println "Adding: ${lib}"
				entries << lib
			}
		}
	}
}

publishing {
	
	repositories {
		maven {
			name = "KInt"
			url = uri(
				(project.version.toString().endsWith("-SNAPSHOT")
					? "https://maven.k-int.com/content/repositories/snapshots"
					: "https://maven.k-int.com/content/repositories/releases")
			)
			credentials {
				username = (project.findProperty("kintMavenUser") ?: System.getenv("KINT_MAVEN_USER")) as String
				password = (project.findProperty("kintMavenPassword") ?: System.getenv("KINT_MAVEN_PASSWORD")) as String
			}
		}
		
		project.gradle.projectsEvaluated {
			all { ArtifactRepository repo ->
					
				String orig = "${repo.url}"
				def altered = orig.replaceAll('(maven\\.k-int\\.com.*\\/)(.+?)$', '$1maven-$2')  
//				println "from ${orig} to ${altered}"
				repo.url = altered
			}
		}
	}
	
	publications {
		maven(MavenPublication) {
			groupId = project.group
			artifactId = project.name
			version = project.version

			from components.java

			pom {
				name = 'Web Toolkit'
				description = 'Provides general tooling for web application frontend development'
				developers {
					developer {
						name = 'Steve Osguthorpe'
						email = 'steve.osguthorpe@k-int.com'
					}
				}
			}
		}
	}
}

compileGsonViews {
	packageName = "web-toolkit"
}

// Writes plugin data
/*
tasks.register("extendedConfigScript") { Task me ->

	// Find the correct file.
	Task configTask = project.tasks.getByName('configScript')
	File configFile = configTask.outputs.files.files.find { it.name == 'config.groovy' }
	if(!configFile) {
		return  // No need to continue if the file is not there
	}

	me.inputs.property('version', project.version)
	me.outputs.file(configFile)
	project.tasks.getByName('compileGroovy').dependsOn( me )
	me.dependsOn( configTask )

	doLast {
		configFile.append("""
    /***** Extention ***** /
withConfig(configuration) {
  inline(phase: 'CONVERSION') { source, context, classNode ->
    classNode.putNodeMetaData('projectVersion', '$project.version')
  }
}""")
	}
}
*/
